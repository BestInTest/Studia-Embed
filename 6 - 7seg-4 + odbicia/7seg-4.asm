CSDS EQU 30H 	; bufor wyboru wskaŸnika 7-segmentowego
CSDB EQU 38H 	; bufor danych wskaŸnika 7-segmentowego

CZAS EQU 79H	; tablica wyœwietlanego czasu
KEYB EQU 76H
SS EQU 075H
MM EQU 074H
HH EQU 073H

ORG 0
	LJMP START
ORG 0BH
	SETB F0
	MOV TH0, #226
	RETI
ORG 40H
START:
	MOV R7, #00000001B	; AKTUALNY WYBRANY WYŒWIETLACZ
	MOV DPTR, #WZORY	; ZAPIS ADRESU WZORÓW DO DPTR
	MOV R4, #0			; AKTUALNIE WYŒWIETLANY SYMBOL
	MOV SS, #46
	MOV MM, #30
	MOV HH, #12
	MOV R1, #CZAS+6
	ACALL PRZELICZ
	MOV TH0, #226
	MOV TMOD, #00110000B
	SETB ET0
	SETB EA
	SETB TR0
	MOV R4, #240
	MOV R3, #4

PETLA:
	JNB F0, PETLA
	CLR F0
	;JESTESMY TU 960x NA SEKUNDE
	ACALL DISPLAY
	DJNZ R4, PETLA 		; TUTAJ JESTESMY 960x NA SEKUNDE
	DJNZ R3, PETLA
	MOV R4, #240
	MOV R3, #4
 	;TUTAJ JESTEM 1x NA SEKUNDE
 	CPL P1.7
 	ACALL UPDATE_TIME
 	SJMP PETLA


DISPLAY:
	MOV R0 , #CSDB 		; PRZENIEŒ DO R0 ADRES CSDB
	MOV A, @R1 			; PRZENIEŒ DO AKUMULATORA AKTUALNIE WYBRANY SYMBOL
	DEC R1
	SETB P1.6			; WY£¥CZAMY WYŒWIETLACZ NA CZAS AKTUALIZACJI
	MOVX @R0, A			; WYSY£AMY WZÓR SEGMENTÓW DO CSDB
	MOV R0 , #CSDS 		; PRZENIEŒ DO R0 ADRES CSDS
	MOV A, R7 			; PRZENIEŒ DO AKUMULATORA WYBRANE WYŒWIETLACZE
	MOVX @R0, A			; WYSY£AMY WZÓR SEGMENTÓW DO CSDS
	CLR P1.6			; W£¥CZAMY WYŒWIETLACZ PO AKTUALIZACJI
	
	JNB P3.5, RLACC
	ORL KEYB, A
RLACC:
	RL A				; ROTACJA W LEWO AKTUALNEGO WYŒWIETLACZA (PRZESUNIÊCIE BITOWE W LEWO)
	JNB ACC.7, NOACC7
	
	MOV A, KEYB
	JZ ZMIANA
	CJNE A, KEYB+1, ZMIANA
	CJNE A, KEYB+2, ZMIANA
	CJNE A, KEYB+3, COSINNEGO
	SJMP ZMIANA
	
COSINNEGO:
	CJNE A, #32, NIELEWO
	INC HH
	ACALL update_timeH
	SJMP KONIECKLAWIATURY

NIELEWO:
	CJNE A, #16, NIEDOL
	INC MM
	ACALL update_timeM
	SJMP KONIECKLAWIATURY

NIEDOL:
	CJNE A, #4, KONIECKLAWIATURY
	INC SS
	
	MOV R4, #240
	MOV R3, #4

KONIECKLAWIATURY:
	ACALL update_timeS

ZMIANA:
	MOV KEYB+3, KEYB+2
	MOV KEYB+2, KEYB+1
	MOV KEYB+1, KEYB
	MOV KEYB, #0

	MOV A, #00000001B
	MOV R1, #CZAS+6

NOACC7:
	MOV R7, A			; ZAPIS WYBRANEGO WYŒWIETLACZA PO ROTACJI
	RET

UPDATE_TIME:
	INC SS
update_timeS:
	MOV A, SS
	CJNE A, #60, ACCNIE60
	MOV SS, #0
	INC MM
update_timeM:
	MOV A, MM
	CJNE A, #60, ACCNIE60
	MOV MM, #0
	INC HH
update_timeH:
	MOV A, HH
	CJNE A, #24, ACCNIE60
	MOV HH, #0

ACCNIE60:
	;ACALL PRZELICZ

PRZELICZ:
	MOV A, SS
	MOV B, #10
	DIV AB
	MOVC A, @A+DPTR
	MOV CZAS+5, A
	MOV A, B
	MOVC A, @A+DPTR
	MOV CZAS+6, A
	MOV A, MM
	MOV B, #10
	DIV AB
	MOVC A, @A+DPTR
	MOV CZAS+3, A
	MOV A, B
	MOVC A, @A+DPTR
	MOV CZAS+4, A
	MOV A, HH
	MOV B, #10
	DIV AB
	MOVC A, @A+DPTR
	MOV CZAS+1, A
	MOV A, B
	MOVC A, @A+DPTR
	MOV CZAS+2, A
	;MOV CZAS+0, #255
	RET

WZORY:
DB 00111111B, 00000110B, 01011011B, 01001111B ; 0-3
DB 01100110B, 01101101B, 01111101B, 00000111B ; 4-7
DB 01111111B, 01101111B, 01110111B, 01111100B ; 8-B
DB 01011000B, 01011110B, 01111001B, 01110001B ; C-F
END